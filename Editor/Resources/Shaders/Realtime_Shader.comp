#version 450 core

#define TWO_PI   6.28318530718
#define PI       3.14159265359
#define INV_PI   0.31830988618
#define DEG_RAD  0.01745329252
#define PHI      1.61803398874

#define MAX_DIST 10000
#define EPSILON  0.0001

struct Triangle {
	vec4 v0;
	vec4 v1;
	vec4 v2;
};

struct Ray {
	vec3 origin;
	vec3 direction;
};

struct Hit {
	float t_dist;
	vec3    pos;
	vec3    nor;
	vec3    uvw;
	Triangle tri;
};

// INPUT ------------------------------------------------------------------------------------------
// ------------------------------------------------------------------------------------------------
layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout(rgba8, binding = 0) uniform image2D screen;

layout(std430, binding = 1) buffer TriangleDataBuffer {
	Triangle triangles[];
};

uniform vec3 camera_pos;
uniform vec3 camera_p_uv;
uniform vec3 camera_p_u;
uniform vec3 camera_p_v;

// FUNCTIONS --------------------------------------------------------------------------------------
//-------------------------------------------------------------------------------------------------

float f_mapfloat(float from_min, float from_max, float to_min, float to_max, float value) {
	return (to_min + ((to_max - to_min) / (from_max - from_min)) * (value - from_min));
}

bool f_rayTriangleIntersection(in Ray ray, in Triangle tri, inout float ray_length, out vec3 normal) {
	vec3 v1v0 = tri.v1.xyz - tri.v0.xyz;
	vec3 v2v0 = tri.v2.xyz - tri.v0.xyz;
	vec3 rov0 = ray.origin - tri.v0.xyz;

	normal = cross(v1v0, v2v0);
	vec3  q = cross(rov0, ray.direction);
	float d = 1 / dot(ray.direction, normal);
	float u = d * dot(-q, v2v0);
	float v = d * dot(q, v1v0);
	float t = d * dot(-normal, rov0);

	if (u < 0 || v < 0 || (u + v) > 1)
		return false;

	ray_length = t;
	return true;
}

vec3 f_barycentricCoords(in vec3 hit_pos, in Triangle tri) {
	vec3 v0 = tri.v1.xyz - tri.v0.xyz;
	vec3 v1 = tri.v2.xyz - tri.v0.xyz;
	vec3 v2 = hit_pos - tri.v0.xyz;

	float d00 = dot(v0, v0);
	float d01 = dot(v0, v1);
	float d11 = dot(v1, v1);
	float d20 = dot(v2, v0);
	float d21 = dot(v2, v1);

	float denom = d00 * d11 - d01 * d01;

	float v = (d11 * d20 - d01 * d21) / denom;
	float w = (d00 * d21 - d01 * d20) / denom;
	float u = 1 - v - w;

	return vec3(u, v, w);
}

// SCENE --------------------------------------------------------------------------------------
// ------------------------------------------------------------------------------------------------
Hit f_SceneIntersection(in Ray ray) {
	Hit hit_data;
	hit_data.t_dist = MAX_DIST;
	float t_dist = MAX_DIST;
	vec3  t_normal;
	for (uint i = 0; i < triangles.length(); i++) {
		if (f_rayTriangleIntersection(ray, triangles[i], t_dist, t_normal)) {
			if (t_dist < hit_data.t_dist && t_dist > EPSILON) {
				hit_data.t_dist = t_dist;
				hit_data.pos = ray.origin + ray.direction * t_dist;
				hit_data.nor = normalize(t_normal);
				hit_data.uvw = f_barycentricCoords(hit_data.pos, triangles[i]);
				hit_data.tri = triangles[i];
			}
		}
	}
	return hit_data;
}

// MAIN FUNCTIONS ---------------------------------------------------------------------------------
// ------------------------------------------------------------------------------------------------

Ray f_cameraRay(vec2 uv) {
	return Ray(camera_pos, normalize(camera_p_uv + (camera_p_u * uv.x) + (camera_p_v * uv.y)  - camera_pos));
}

vec3 f_pathTrace(in Ray ray) {
	Hit hit_data = f_SceneIntersection(ray);
	if (hit_data.t_dist != MAX_DIST)
		return vec3(f_mapfloat(0, 1, 0.3, 0.9, abs(dot(hit_data.nor, ray.direction)))) * pow((hit_data.t_dist + 1.0), -0.2) ;//* hit_data.uvw ;
	return vec3(0.1);
}

void main() {
	ivec2 work_id = ivec2(gl_GlobalInvocationID.xy);
	ivec2 resolution = imageSize(screen);

	vec2 uv = (vec2(work_id) - 1.0 - vec2(resolution) / 2.0) / float(max(resolution.x, resolution.y));
	Ray ray = f_cameraRay(uv);
	imageStore(screen, work_id, vec4(f_pathTrace(ray), 1));
}